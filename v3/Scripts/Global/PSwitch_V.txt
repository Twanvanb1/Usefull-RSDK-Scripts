// ----------------------------------
// RSDK Project: Sonic CD's scrapped rounds
// Script Description: Plane Sw V Object
// Script Author: Twanvanb1
// ----------------------------------

// ========================
// Aliases
// ========================

#alias object.value0 : object.extendTop
#alias object.value1 : object.extendBottom
#alias object.value2 : object.planeR
#alias object.value3 : object.planeL
#alias object.value4 : object.drawOrderR
#alias object.value5 : object.drawOrderL
#alias object.value6 : object.onGround

// Gravity
#alias 0	:	GRAVITY_GROUND
#alias 1	:	GRAVITY_AIR

// ========================
// subs
// ========================

sub ObjectPlayerInteraction
	CheckEqual(object.onGround, false)
	TempValue0 = checkResult
	CheckEqual(player.gravity, GRAVITY_GROUND)
	TempValue0 |= checkResult
	
	if TempValue0 == true
		PlayerObjectCollision(C_TOUCH, -12, object.extendTop, 12, object.extendBottom)
		if checkResult == true
			if player.XVelocity > 0
				GetBit(TempValue0, object.propertyValue, 6)
				if TempValue0 == false
					player.collisionPlane = object.planeL
				end if
				Player.DrawOrder = object.drawOrderL
			else
				GetBit(TempValue0, object.propertyValue, 6)
				if TempValue0 == false
					player.collisionPlane = object.planeR
				end if
				Player.DrawOrder = object.drawOrderR
			end if
		end if
	end if
end sub

sub ObjectDraw
	// Only draw if the player has debug mode turned on
	if Stage.DebugMode == true
		TempValue1 = object.propertyValue
		TempValue1 &= 3
		TempValue1 += 2
		TempValue0 = 1
		TempValue0 <<= TempValue1
		TempValue1 = TempValue0
		TempValue0 <<= 19
		FlipSign(TempValue0)
		TempValue0 += object.ypos
		TempValue0 += 0x80000
		GetBit(TempValue2, object.propertyValue, 7)
		TempValue3 = object.propertyValue
		TempValue3 >>= 2
		TempValue3 &= 15
		GetBit(TempValue4, object.propertyValue, 6)
		if TempValue4 == true
			TempValue3 >>= 2
			TempValue3 += 16
		end if
		
		TempValue4 = 0
		while TempValue4 < TempValue1
			if TempValue2 == true
				DrawSpriteXY(20, object.xpos, TempValue0)
			end if
			DrawSpriteXY(TempValue3, object.xpos, TempValue0)
			TempValue0 += 0x100000
			TempValue4++
		loop

		if TempValue1 == 0
			DrawSprite(TempValue3)
		end if
	end if
end sub

sub ObjectStartup
	LoadSpriteSheet("Global/Editor.gif")
	
	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 65)
		TempValue0++
		TempValue1 += 17
	loop
	
	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 82)
		TempValue0++
		TempValue1 += 17
	loop

	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 99)
		TempValue0++
		TempValue1 += 17
	loop

	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 116)
		TempValue0++
		TempValue1 += 17
	loop

	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 133)
		TempValue0++
		TempValue1 += 17
	loop
	
	SpriteFrame(-8, -8, 16, 16, 103, 65) // #20 - back

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Plane Sw V]
			TempValue0 = object[arrayPos0].propertyValue
			TempValue0 &= 3
			TempValue0 += 2
			object[arrayPos0].extendTop = 1
			object[arrayPos0].extendTop <<= TempValue0
			object[arrayPos0].extendTop <<= 3
			object[arrayPos0].extendBottom = object[arrayPos0].extendTop
			FlipSign(object[arrayPos0].extendTop)

			GetBit(object[arrayPos0].planeR, object[arrayPos0].propertyValue, 2)
			GetBit(object[arrayPos0].planeL, object[arrayPos0].propertyValue, 3)

			GetBit(object[arrayPos0].drawOrderR, object[arrayPos0].propertyValue, 4)
			object[arrayPos0].drawOrderR += 4
			GetBit(object[arrayPos0].drawOrderL, object[arrayPos0].propertyValue, 5)
			object[arrayPos0].drawOrderL += 4

			GetBit(object[arrayPos0].onGround, object[arrayPos0].propertyValue, 7)
		end if
		ArrayPos0++
	loop
end sub

// ========================
// Editor subs & Aliases
// ========================

#alias 0 : VAR_ID_SIZE
#alias 1 : VAR_ID_PLANEL
#alias 2 : VAR_ID_PLANER
#alias 3 : VAR_ID_DRAWL
#alias 4 : VAR_ID_DRAWR
#alias 5 : VAR_ID_ONLYDRAWORDER
#alias 6 : VAR_ID_ONGROUND


sub RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
			
		case VAR_ID_SIZE // size
			checkResult = object.propertyValue
			checkResult &= 3
			break
			
		case VAR_ID_PLANEL // collisionPlaneL
			GetBit(checkResult, object.propertyValue, 2)
			break
			
		case VAR_ID_PLANER // collisionPlaneR
			GetBit(checkResult, object.propertyValue, 3)
			break
			
		case VAR_ID_DRAWL // drawOrderL
			GetBit(checkResult, object.propertyValue, 4)
			break
			
		case VAR_ID_DRAWR // drawOrderR
			GetBit(checkResult, object.propertyValue, 5)
			break
			
		case VAR_ID_ONLYDRAWORDER // onlyDrawOrder
			GetBit(checkResult, object.propertyValue, 6)
			break
			
		case VAR_ID_ONGROUND // onGround
			GetBit(checkResult, object.propertyValue, 7)
			break
			
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
			
		case VAR_ID_SIZE // size
			TempValue1 = 3
			Not(TempValue1)
			object.propertyValue &= TempValue1

			TempValue0 = editor.variableValue
			TempValue0 &= 3
			object.propertyValue |= TempValue0
			break
			
		case VAR_ID_PLANEL // collisionPlaneL
			CheckNotEqual(editor.variableValue, false)
			SetBit(object.propertyValue, 2, checkResult)
			break
			
		case VAR_ID_PLANER // collisionPlaneR
			CheckNotEqual(editor.variableValue, false)
			SetBit(object.propertyValue, 3, checkResult)
			break
			
		case VAR_ID_DRAWL // drawOrderL
			CheckNotEqual(editor.variableValue, false)
			SetBit(object.propertyValue, 4, checkResult)
			break
			
		case VAR_ID_DRAWR // drawOrderR
			CheckNotEqual(editor.variableValue, false)
			SetBit(object.propertyValue, 5, checkResult)
			break
			
		case VAR_ID_ONLYDRAWORDER // onlyDrawOrder
			CheckNotEqual(editor.variableValue, false)
			SetBit(object.propertyValue, 6, checkResult)
			break
			
		case VAR_ID_ONGROUND // onGround
			CheckNotEqual(editor.variableValue, false)
			SetBit(object.propertyValue, 7, checkResult)
			break
			
		end switch
	end if
end sub


sub RSDKDraw
	// size
	TempValue1 = object.propertyValue
	TempValue1 &= 3
	TempValue1 += 2

	TempValue0 = 1
	TempValue0 <<= TempValue1
	TempValue1 = TempValue0
	TempValue0 <<= 19
	FlipSign(TempValue0)
	TempValue0 += object.ypos
	TempValue0 += 0x80000

	// onGround
	GetBit(TempValue2, object.propertyValue, 7)
	TempValue3 = object.propertyValue
	TempValue3 >>= 2
	TempValue3 &= 15

	// onlyDrawOrder
	GetBit(TempValue4, object.propertyValue, 6)
	if TempValue4 == true
		TempValue3 >>= 2
		TempValue3 += 16
	end if

	TempValue4 = 0
	while TempValue4 < TempValue1
		if TempValue2 == true
			DrawSpriteXY(20, object.xpos, TempValue0)
		end if
		DrawSpriteXY(TempValue3, object.xpos, TempValue0)
		TempValue0 += 0x100000
		TempValue4++
	loop

	if TempValue1 == 0
		DrawSprite(TempValue3)
	end if
end sub


sub RSDKLoad
	LoadSpriteSheet("Global/Editor.gif")
	
	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 65)
		TempValue0++
		TempValue1 += 17
	loop
	
	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 82)
		TempValue0++
		TempValue1 += 17
	loop

	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 99)
		TempValue0++
		TempValue1 += 17
	loop

	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 116)
		TempValue0++
		TempValue1 += 17
	loop

	TempValue0 = 0
	TempValue1 = 35
	while TempValue0 < 4
		SpriteFrame(-8, -8, 16, 16, TempValue1, 133)
		TempValue0++
		TempValue1 += 17
	loop
	
	SpriteFrame(-8, -8, 16, 16, 103, 65) // #20 - back

	AddEditorVariable("size")
	SetActiveVariable("size")
	AddEnumVariable("4 Nodes", 0)
	AddEnumVariable("8 Nodes", 1)
	AddEnumVariable("16 Nodes", 2)
	AddEnumVariable("32 Nodes", 3)

	AddEditorVariable("collisionPlaneL")
	SetActiveVariable("collisionPlaneL")
	AddEnumVariable("Plane A", 0)
	AddEnumVariable("Plane B", 1)

	AddEditorVariable("collisionPlaneR")
	SetActiveVariable("collisionPlaneR")
	AddEnumVariable("Plane A", 0)
	AddEnumVariable("Plane B", 1)

	AddEditorVariable("drawOrderL")
	SetActiveVariable("drawOrderL")
	AddEnumVariable("Low Layer", 0)
	AddEnumVariable("High Layer", 1)

	AddEditorVariable("drawOrderR")
	SetActiveVariable("drawOrderR")
	AddEnumVariable("Low Layer", 0)
	AddEnumVariable("High Layer", 1)

	AddEditorVariable("onlyDrawOrder")
	SetActiveVariable("onlyDrawOrder")
	AddEnumVariable("false", false)
	AddEnumVariable("true", true)

	AddEditorVariable("onGround")
	SetActiveVariable("onGround")
	AddEnumVariable("false", false)
	AddEnumVariable("true", true)
end sub
